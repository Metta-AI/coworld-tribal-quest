import std/[json, os]

import bitworld/protocol
import tribal_quest/fortress_engine

const RootDir = currentSourcePath.parentDir.parentDir

template expectValueError(body: untyped, message: string) =
  var rejected = false
  try:
    body
  except ValueError:
    rejected = true
  doAssert rejected, message

proc testFortressRuntimeIsRequired() =
  validateWorldRuntime("fortress")
  validateWorldRuntime("fortress-engine")
  validateWorldRuntime("fortress_engine")

  expectValueError(validateWorldRuntime(""), "empty runtime must not be accepted")
  expectValueError(validateWorldRuntime("local"), "local runtime must be rejected")
  expectValueError(validateWorldRuntime("quest"), "unknown runtime must be rejected")

proc testAdventurerInputPayloads() =
  let mask = ButtonUp or ButtonRight or ButtonA
  let input = parseJson(adventurerInputJson(mask))
  doAssert input["type"].getStr() == AdventurerInputType
  doAssert input["control_profile"].getStr() == AdventurerControlProfile
  doAssert input["move"].getStr() == "NE"
  doAssert input["attack"].getBool()
  doAssert not input["use"].getBool()
  doAssert input["buttons"]["up"].getBool()
  doAssert input["buttons"]["right"].getBool()
  doAssert input["buttons"]["a"].getBool()
  doAssert not input["buttons"]["b"].getBool()

  let conflict = parseJson(adventurerInputJson(ButtonUp or ButtonDown or ButtonB))
  doAssert conflict["move"].getStr() == "none",
    "opposed vertical inputs should cancel for Fortress adventurer movement"
  doAssert conflict["use"].getBool()

  let raw = parseJson(adventurerRawActionJson(17))
  doAssert raw["type"].getStr() == AdventurerInputType
  doAssert raw["control_profile"].getStr() == AdventurerControlProfile
  doAssert raw["raw_action"].getInt() == 17

proc testAdventurerObservationParsing() =
  let observation = parseFortressAdventurerObservation($(%*{
    "agent_id": 42,
    "team_id": 2,
    "civilization": "Elf",
    "role": "scout",
    "position": {"x": 13, "y": 17},
    "hp": 5,
    "max_hp": 9,
    "status": "ready",
    "view_plane": {
      "origin": {"x": 8, "y": 12},
      "width": QuestAdventureCropTiles,
      "height": QuestAdventureCropTiles
    }
  }))
  doAssert observation.agentId == 42
  doAssert observation.teamId == 2
  doAssert observation.civilization == "Elf"
  doAssert observation.role == "scout"
  doAssert observation.x == 13 and observation.y == 17
  doAssert observation.hp == 5 and observation.maxHp == 9
  doAssert observation.status == "ready"
  doAssert observation.originX == 8 and observation.originY == 12
  doAssert observation.cropWidth == QuestAdventureCropTiles
  doAssert observation.cropHeight == QuestAdventureCropTiles

  let legacyCrop = parseFortressAdventurerObservation($(%*{
    "agentId": 5,
    "teamId": 1,
    "x": 20,
    "y": 21,
    "crop": {
      "origin_x": 15,
      "origin_y": 16,
      "width": 7,
      "height": 9
    }
  }))
  doAssert legacyCrop.agentId == 5
  doAssert legacyCrop.teamId == 1
  doAssert legacyCrop.x == 20 and legacyCrop.y == 21
  doAssert legacyCrop.originX == 15 and legacyCrop.originY == 16
  doAssert legacyCrop.cropWidth == 7 and legacyCrop.cropHeight == 9

proc testFortressEngineConfigValidation() =
  let config = defaultFortressEngineConfig()
  doAssert config.path.len == 0
  doAssert config.adventurerRole == "adventurer"
  doAssert config.worldWidth == FortressWorldWidthTiles
  doAssert config.worldHeight == FortressWorldHeightTiles
  doAssert config.townAgentsPerTeam == FortressTownAgentsPerTeam
  doAssert config.adventurerSlots == FortressAdventurerSlots

  expectValueError(validateAdventurerSlot(-1), "negative slot must be rejected")
  expectValueError(
    validateAdventurerSlot(FortressAdventurerSlots),
    "Fortress adventurer slots should be capped at 64"
  )
  validateAdventurerSlot(FortressAdventurerSlots - 1)

  var blankPath = config
  expectValueError(
    blankPath.validateFortressEngineConfig(),
    "fortress mode requires a concrete engine path"
  )

  let tempRoot = getTempDir() / "tribal_quest_fortress_engine_test"
  if dirExists(tempRoot):
    removeDir(tempRoot)
  createDir(tempRoot)
  createDir(tempRoot / "tribal_village_env")
  createDir(tempRoot / "tribal_village_env" / "coworld")
  writeFile(
    tempRoot / "pyproject.toml",
    "[project]\nname='coworld-tribal-fortress'\n"
  )
  writeFile(tempRoot / "tribal_village_env" / "coworld" / "server.py", "")
  doAssert tempRoot.isLikelyFortressEngineCheckout()

  var engineConfig = config
  engineConfig.path = tempRoot
  engineConfig.validateFortressEngineConfig()
  removeDir(tempRoot)

proc testDefaultFortressPath() =
  let expected = RootDir / DefaultFortressCheckoutDir
  doAssert defaultFortressEnginePath(RootDir) == expected

when isMainModule:
  testFortressRuntimeIsRequired()
  testAdventurerInputPayloads()
  testAdventurerObservationParsing()
  testFortressEngineConfigValidation()
  testDefaultFortressPath()
  echo "All tests passed"
